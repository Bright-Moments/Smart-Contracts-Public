//SPDX-License-Identifier: MIT
/*@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@          @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@              @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                 @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                  @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                     @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@ @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                           @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                         @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                      @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                                @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                        @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                                   @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                             @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@                    @@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*/
/** 
 * @author @brougkr
 * @title Launchpad
 * - Launchpad is an interface to easily create NFT Projects, Dutch Auctions, English Auctions, Waitlists, Merkle Claims, & Mint Passes etc. on EVM
 * - Optionally Includes Integration And Deployment With Multiple Contracts
 * - Live-Rebate Respsend & Discount Dutch Auctions, English Auctions, LiveMint, MintPass Factory, ArtBlocks Flex, Core, & Poly Minters
 * - It Interacts With The Following Contracts Optionally, Depending On Your Project Needs:
 * - { 1 } - { Dutch Auction Marketplace }
 * - { 2 } - { English Auction Marketplace }
 * - { 3 } - { Fixed Sale & Presale Marketplace }
 * - { 4 } - { LiveMint }
 * - { 5 } - { Custom Minters or ArtBlocks Minters }
 * - { 6 } - { Minted Works Factory }
 * - { 7 } - { MintPass Factory }
 * - { 8 } - { Merkle Claimer }
 * - { 9 } - { Launchpad Registry }
 * - { 10 } - { Waitlist }
*/
pragma solidity 0.8.19;
import { MissionControl } from "./MissionControl.sol";
contract Launchpad is MissionControl
{   
    struct StateParameters 
    {
        bool _Active;                // [0] -> _Active
        address _DutchMarketplace;   // [1] -> _DutchMarketplace
        address _DynamicMarketplace; // [2] -> _DynamicMarketplace
        address _EnglishMarketplace; // [3] -> _EnglishMarketplace
        address _LiveMint;           // [4] -> _LiveMint
        address _FactoryMintedWorks; // [5] -> _FactoryMintedWorks
        address _FactoryMintPass;    // [6] -> _FactoryMintPass
        address _LaunchpadRegistry;  // [7] -> _LaunchpadRegistry
        address _MerkleClaimer;      // [8] -> _MerkleClaimer
        address _Waitlist;           // [9] -> _Waitlist
    }
    
    /*-----------------------------------------------*/
    /*------------------ * STATE * ------------------*/
    /*-----------------------------------------------*/

    StateParameters public Params; // Contract Params

    /*------------------------------------------------*/
    /*------------------ * EVENTS * ------------------*/
    /*------------------------------------------------*/

    /**
     * @dev Emitted When A Launchpad Project Is Created
     * @param LaunchpadProjectID The Launchpad ProjectID
     */
    event ProjectInvoked(uint LaunchpadProjectID);  

    /**
     * @dev Emitted When A Project Is Modified
     * @param LaunchpadProjectID The Launchpad ProjectID
     */
    event ProjectModified(uint LaunchpadProjectID); 

    /**
     * @dev Emitted When LiveMint Is Enabled
     * @param LaunchpadProjectID The Launchpad ProjectID
     */
    event LiveMintEnabled(uint LaunchpadProjectID); 

    /*-----------------------------------------------------*/
    /*------------------ * CONSTRUCTOR * ------------------*/
    /*-----------------------------------------------------*/

    constructor() 
    { 
        _OWNER = msg.sender;
        Params._Active = true; 
    }

    /*---------------------------------------------------------*/
    /*------------------ * ADMIN FUNCTIONS * ------------------*/
    /*---------------------------------------------------------*/

    /**
     * @dev Deploys A New Mint Pass
     * @param ParamsMintPass Mint Pass Parameters
     * @return MintPassAddress The Address Of The New Mint Pass Smart Contract
     */
    function InitMintPass ( IMintPass.Params memory ParamsMintPass ) external onlyOperator returns ( address MintPassAddress )
    { 
        return IMintPass(Params._FactoryMintPass)._InitMintPass(ParamsMintPass); 
    }

    /**
     * @dev Starts A Dutch Auction And Optionally Deploys New MintPass Contract
     * @param ParamsMintPass Mint Pass Parameters
     * @param ParamsSale Marketplace Parameters
     * @param ParamsSaleInternal Marketplace Parameters Cont.
     * @param RootsPriority Merkle Roots Eligibility
     * @param RootsAmounts Merkle Root Amounts
     * @param DiscountAmounts Discount Amounts
     */
    function InitSaleDutch (       
        IMintPass.Params memory ParamsMintPass,                   // Mint Pass Parameters
        IMarketplace.DutchSale memory ParamsSale,                 // Marketplace Parameters
        IMarketplace.DutchSaleInternal memory ParamsSaleInternal, // Marketplace Parameters Cont.
        bytes32[] calldata RootsPriority,                         // Merkle Roots Eligibility
        bytes32[] calldata RootsAmounts,                          // Merkle Root Amounts
        uint[] calldata DiscountAmounts                           // Discount Amounts
    ) external onlyOperator {
        address MintPassAddress = ParamsSaleInternal._NFT;
        if(ParamsSaleInternal._NFT == Params._FactoryMintPass)
        {
            ParamsMintPass._Marketplace = Params._DutchMarketplace;
            MintPassAddress = IMintPass(Params._FactoryMintPass)._InitMintPass(ParamsMintPass); 
            ParamsSaleInternal._NFT = MintPassAddress;
        }
        uint SaleID = IMarketplace(Params._DutchMarketplace)._StartSale(ParamsSale, ParamsSaleInternal, RootsPriority, RootsAmounts, DiscountAmounts);
        uint LaunchpadProjectID = ILaunchpadRegistry(Params._LaunchpadRegistry)._NewProject(ILaunchpadRegistry.Project(
            ParamsSale._Name,             // _Name
            true,                         // _Active
            type(uint).max,               // _LiveMintArtistID
            ParamsMintPass._MaxSupply,    // _MaxSupply
            ParamsMintPass._MintsPerPack, // _MintsPerPack
            SaleID,                       // _SaleID
            MintPassAddress,              // _MintPassAddress
            Params._DutchMarketplace,     // _MarketplaceAddress
            address(0)                    // _MintedWorkAddress
        ));
        emit ProjectInvoked ( LaunchpadProjectID );
    }

    /**
     * @dev Starts A Fixed Price Sale And Optionally Deploys A New MintPass Contract
     * @param ParamsMintPass Mint Pass Parameters
     * @param ParamsSale Marketplace Parameters
     * @param DiscountAmounts Discount Amounts
     */
    function InitSaleFixedPrice (
        IMintPass.Params memory ParamsMintPass,        // Mint Pass Parameters
        IMarketplace.SaleFixedPrice memory ParamsSale, // Params For Sale
        uint[] calldata DiscountAmounts                // Discount Amounts
    ) external onlyOperator {
        address MintPassAddress = ParamsSale._NFT;
        if(ParamsSale._NFT == Params._FactoryMintPass) 
        { 
            ParamsMintPass._Marketplace = Params._DynamicMarketplace;
            MintPassAddress = IMintPass(Params._FactoryMintPass)._InitMintPass(ParamsMintPass); 
            ParamsSale._NFT = MintPassAddress;
        } 
        uint SaleID = IMarketplace(Params._DynamicMarketplace)._StartFixedPriceSale(ParamsSale, DiscountAmounts);
        uint LaunchpadProjectID = ILaunchpadRegistry(Params._LaunchpadRegistry)._NewProject(ILaunchpadRegistry.Project(
            ParamsSale._Name,             // _Name
            true,                         // _Active
            type(uint).max,               // _LiveMintArtistID
            ParamsMintPass._MaxSupply,    // _MaxSupply
            ParamsMintPass._MintsPerPack, // _MintsPerPack
            SaleID,                       // _SaleID
            MintPassAddress,              // _MintPassAddress
            Params._DynamicMarketplace,   // _MarketplaceAddress
            address(0)                    // _MintedWorkAddress
        ));
        emit ProjectInvoked ( LaunchpadProjectID );
    }

    /**
     * @dev Starts A Presale Sale And Optionally Deploys A New MintPass Contract
     * @param ParamsSale SalePresale
     * @param Roots SalePresaleRoots
     * @param ParamsMintPass Mint Pass Parameters
     */
    function InitSalePresale (
        IMintPass.Params memory ParamsMintPass,     // Mint Pass Parameters
        IMarketplace.SalePresale memory ParamsSale, // SalePresale
        IMarketplace.SalePresaleRoots memory Roots  // SalePresaleRoots
    ) external onlyOperator {
        address MintPassAddress;
        if(ParamsSale._NFT == Params._FactoryMintPass) 
        { 
            ParamsMintPass._Marketplace = Params._DynamicMarketplace;
            MintPassAddress = IMintPass(Params._FactoryMintPass)._InitMintPass(ParamsMintPass); 
            ParamsSale._NFT = MintPassAddress;
        }
        uint SaleID = IMarketplace(Params._DynamicMarketplace)._StartPresaleSale(ParamsSale, Roots);
        uint LaunchpadProjectID = ILaunchpadRegistry(Params._LaunchpadRegistry)._NewProject(ILaunchpadRegistry.Project(
            ParamsSale._Name,             // _Name
            true,                         // _Active
            type(uint).max,               // _LiveMintArtistID
            ParamsMintPass._MaxSupply,    // _MaxSupply
            ParamsMintPass._MintsPerPack, // _MintsPerPack
            SaleID,                       // _SaleID
            MintPassAddress,              // _MintPassAddress
            Params._DynamicMarketplace,   // _MarketplaceAddress
            address(0)                    // _MintedWorkAddress
        ));
        emit ProjectInvoked ( LaunchpadProjectID );
    }

    /**
     * @dev Starts An English Auction And Optionally Deploys A New MintPass Contract
     * @param ParamsMintPass Mint Pass Parameters
     * @param ParamsAuction Auction Parameters
     * @param MerkleRootsEligibilities Merkle Roots Eligibility
     * @param MerkleRootsAmounts Merkle Roots Amounts
     * @param DiscountAmounts Discount Amounts
     */
    function InitSaleEnglishAuction (
        IMintPass.Params memory ParamsMintPass,      // Mint Pass Parameters
        IAuction.Params memory ParamsAuction,        // Auction
        bytes32[] calldata MerkleRootsEligibilities, // Merkle Roots Eligibility
        bytes32[] calldata MerkleRootsAmounts,       // Merkle Roots Amounts
        uint[] calldata DiscountAmounts              // Discount Amounts
    ) external onlyOperator {
        address MintPassAddress;
        if(ParamsAuction._NFT == Params._FactoryMintPass)
        {
            ParamsMintPass._Marketplace = Params._EnglishMarketplace;
            MintPassAddress = IMintPass(Params._FactoryMintPass)._InitMintPass(ParamsMintPass);
            ParamsAuction._NFT = MintPassAddress;
        }
        uint SaleID = IAuction(Params._EnglishMarketplace)._StartAuction(ParamsAuction, MerkleRootsEligibilities, MerkleRootsAmounts, DiscountAmounts);
        uint LaunchpadProjectID = ILaunchpadRegistry(Params._LaunchpadRegistry)._NewProject(ILaunchpadRegistry.Project(
            ParamsAuction._Name,          // _Name
            true,                         // _Active
            type(uint).max,               // _LiveMintArtistID
            ParamsMintPass._MaxSupply,    // _MaxSupply
            ParamsMintPass._MintsPerPack, // _MintsPerPack
            SaleID,                       // _SaleID
            MintPassAddress,              // _MintPassAddress
            Params._EnglishMarketplace,   // _MarketplaceAddress
            address(0)                    // _MintedWorkAddress
        ));
        emit ProjectInvoked ( LaunchpadProjectID );
    }

    /**
     * @dev Initializes A New Live Mint Project
     * @param ParamsMinter The Live Mint Parameters
     */
    function InitLiveMintArtist ( ILiveMint.Params memory ParamsMinter ) external onlyOperator 
    {
        uint LiveMintArtistID = ILiveMint(Params._LiveMint)._InitLiveMintArtist(ParamsMinter);
        uint LaunchpadProjectID = ILaunchpadRegistry(Params._LaunchpadRegistry)._NewProject(ILaunchpadRegistry.Project(
            ParamsMinter._Name,              // _Name
            true,                            // _Active
            LiveMintArtistID,                // _LiveMintArtistID
            ParamsMinter._MaxSupply,         // _MaxSupply
            type(uint).max,                  // _MintsPerPack
            type(uint).max,                  // _SaleID
            ParamsMinter._MintPass,          // _MintPassAddress
            address(0),                      // _MarketplaceAddress       
            ParamsMinter._Minter             // _MintedWorkAddress
        ));
        emit ProjectInvoked ( LaunchpadProjectID );
    }

    /**
     * @dev Initializes A New Merkle Claimer At A New Claim Index
     * @param _NewClaim The Merkle Claimer Claim Parameters
     */
    function MerkleClaimerStart(IMerkle.Claim memory _NewClaim) external onlyOperator { IMerkle(Params._MerkleClaimer)._NewClaim(_NewClaim); }

    /**
     * @dev Changes The Current Merkle Root At The Provided Claim Index
     * @param ClaimIndex The Claim Index To Change The Root For
     * @param Root The New Merkle Root
     */
    function MerkleClaimerNewRoot ( uint ClaimIndex, bytes32 Root ) external onlyOperator { IMerkle(Params._MerkleClaimer)._ChangeRoot(ClaimIndex, Root); }

    /**
     * @dev Clears A Dutch Auction & Initiates Refunds & Proceeds
     * @param LaunchpadProjectID The Index Of The Sale To Initiate Refunds & Proceeds For
     */
    function FinalizeDutchAuction ( uint LaunchpadProjectID ) external onlyOperator 
    { 
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IMarketplace(Params._DutchMarketplace)._InitiateRefundsAndProceeds(SaleIndex); 
    }

    /**
     * @dev Initiates Withdrawl Proceeds For The Entire Auction & Disperses NFTs To The Top Bidders On The Leaderboard (First-Come-First-Serve) (When TokenID Is Ambiguous)
     * @param LaunchpadProjectID The Launchpad ProjectID To Finalize
     */
    function FinalizeEnglishAuctionTokenIDsAmbiguous ( uint LaunchpadProjectID ) external onlyOperator 
    { 
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IAuction(Params._EnglishMarketplace)._ProcessETHAndNFTsTokenIDsAmbiguous(SaleIndex); 
    }

    /**
     * @dev Initiates Withdrawl Proceeds For The Entire Auction & Disperses NFTs To The Top Bidders On The Leaderboard (Ascending Ranking) (When TokenID Matters)
     * @param LaunchpadProjectID The Launchpad Project ID To Finalize
     */
    function FinalizeEnglishAuctionTokenIDsDistinct ( uint LaunchpadProjectID ) external onlyOperator 
    { 
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IAuction(Params._EnglishMarketplace)._ProcessETHAndNFTsTokenIDsDistinct(SaleIndex); 
    }

    /**
     * @dev Initiates Withdrawl Proceeds For The Entire Auction & Disperses NFTs To The Ranked Top Bidders On The Leaderboard (When TokenIDs Are Unique) 
     * @param LaunchpadProjectID The Launchpad ProjectID To Finalize
     * @param TokenIDs The TokenIDs To Disperse To The Top Bidders On The Leaderboard (In Order Of Ranked Leaderboard Position)
     */
    function FinalizeEnglishAuctionTokenIDsDistinctRanked ( uint LaunchpadProjectID, uint[] calldata TokenIDs ) external onlyOperator
    {
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IAuction(Params._EnglishMarketplace)._ProcessETHAndNFTsTokenIDsSpecificRanked(SaleIndex, TokenIDs);
    }

    /**
     * @dev Initiates Withdrawl Proceeds For The Entire Auction & Disperses NFTs To The Top Bidders On The Leaderboard With Configured Mint Pack Amount In The Auction Marketplace SaleID
     * @param LaunchpadProjectID The Launchpad ProjectID To Finalize
     */
    function FinalizeEnglishAuctionMintPacks ( uint LaunchpadProjectID ) external onlyOperator
    {
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IAuction(Params._EnglishMarketplace)._ProcessETHAndNFTsMintPacksRanked(SaleIndex);
    }

    /**
     * @dev Withdraws All ETH From The Marketplace To The Multisig
     * @param LaunchpadProjectID The Launchpad ProjectID To Finalize
     */
    function FinalizeDynamicMarketplaceSale ( uint LaunchpadProjectID ) external onlyOperator 
    { 
        uint SaleIndex = ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleIndex(LaunchpadProjectID);
        IMarketplace(Params._DynamicMarketplace)._WithdrawETHFromSaleIndex(SaleIndex); 
    }

    /**
     * @dev Starts A New Waitlist
     * @param Root The Merkle Root For The Waitlist
     */
    function WaitlistStart ( string calldata Name, bytes32 Root ) external onlyOperator { IWaitlist(Params._Waitlist)._NewWaitlist(Name, Root); }

    /**
     * @dev Changes A Waitlist Root
     * @param WaitlistIndex The Index Of The Waitlist To Change The Root For
     * @param Root The New Merkle Root
     */
    function WaitlistChange ( uint WaitlistIndex, bytes32 Root ) external onlyOperator { IWaitlist(Params._Waitlist)._NewWaitlistRoot(WaitlistIndex, Root); }

    /*---------------------------------------------------------*/
    /*------------------ * OWNER FUNCTIONS * ------------------*/
    /*---------------------------------------------------------*/

    /**
     * @dev Toggles The Launchpad Overall Contract State
     */
    function __ActiveToggle() external onlyOwner { Params._Active = !Params._Active; }

    /**
     * @dev Changes Marketplace Address
     * @param NewAddress The New Marketplace Address
     */
    function __ChangeMarketplace(address NewAddress) external onlyOwner { Params._DutchMarketplace = NewAddress; }

    /**
     * @dev Changes LiveMint Address
     * @param NewAddress The New LiveMint Address
     */
    function __ChangeLiveMint(address NewAddress) external onlyOwner { Params._LiveMint = NewAddress; }

    /**
     * @dev Changes Mint Pass Factory Address
     * @param NewAddress The New Mint Pass Factory Address
     */
    function __ChangeFactoryMintPass(address NewAddress) external onlyOwner { Params._FactoryMintPass = NewAddress; }
    
    /**
     * @dev Changes Minted Works Factory Address
     * @param NewAddress The New Minted Works Factory Address
     */
    function __ChangeFactoryMintedWorks(address NewAddress) external onlyOwner { Params._FactoryMintedWorks = NewAddress; }

    /**
     * @dev Changes Basic Marketplace Address
     * @param NewAddress The New Basic Marketplace Address
     */
    function __ChangeBasicMarketplace(address NewAddress) external onlyOwner { Params._DynamicMarketplace = NewAddress; }

    /**
     * @dev Changes English Auction Address
     * @param NewAddress The New English Auction Address
     */
    function __ChangeAuctionMarketplace(address NewAddress) external onlyOwner { Params._EnglishMarketplace = NewAddress; }

    /**
     * @dev Changes The Merkle Claimer Address
     * @param NewAddress The New Merkle Claimer Address
     */
    function __ChangeMerkleClaimer(address NewAddress) external onlyOwner { Params._MerkleClaimer = NewAddress; }

    /**
     * @dev Upgrades The Dynamic Marketplace's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeDynamicMarketplace(address NewAddress) external onlyOwner { ICustom(Params._DynamicMarketplace).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Dutch Marketplace's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeDutchMarketplace(address NewAddress) external onlyOwner { ICustom(Params._DutchMarketplace).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The LiveMint's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeLiveMint(address NewAddress) external onlyOwner { ICustom(Params._LiveMint).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The English Auction's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeEnglishAuction(address NewAddress) external onlyOwner { ICustom(Params._EnglishMarketplace).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Minted Works Factory's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeFactoryMintedWorks(address NewAddress) external onlyOwner { ICustom(Params._FactoryMintedWorks).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Mint Pass Factory's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeFactoryMintPass(address NewAddress) external onlyOwner { ICustom(Params._FactoryMintPass).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Launchpad Registry's Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeLaunchpadRegistry(address NewAddress) external onlyOwner { ICustom(Params._LaunchpadRegistry).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Auction Marketplace Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeAuctionMarketplace(address NewAddress) external onlyOwner { ICustom(Params._LaunchpadRegistry).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Upgrades The Merkle Claimer Active Launchpad Address
     * @param NewAddress The New Launchpad Address
     */
    function __UpgradeMerkleClaimer(address NewAddress) external onlyOwner { ICustom(Params._MerkleClaimer).____LaunchpadOverrideAddress(NewAddress); }

    /**
     * @dev Initiates Upgrade Of The Launchpad With All Current Active Contracts
     * @param NewLaunchpadAddress The New Launchpad Address
     */
    function ___UpgradeAll(address NewLaunchpadAddress) external onlyOwner
    {
        ICustom(Params._DutchMarketplace).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._DynamicMarketplace).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._EnglishMarketplace).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._LiveMint).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._FactoryMintedWorks).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._FactoryMintPass).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._LaunchpadRegistry).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._MerkleClaimer).____LaunchpadOverrideAddress(NewLaunchpadAddress);
        ICustom(Params._Waitlist).____LaunchpadOverrideAddress(NewLaunchpadAddress);
    }

    /**
     * @dev Instantiates A New State
     * @param _State The New State Parameters
     */
    function ___NewState(StateParameters memory _State) external onlyOwner { Params = _State; }

    /**
     * @dev Executes Arbitrary Transaction(s)
     * @param Targets The Addresses To Execute Transactions On
     * @param Values The Values To Send In Wei
     * @param Datas The Data To Execute
     */
    function ___InitTransactions(address[] memory Targets, uint[] memory Values, bytes[] memory Datas) external onlyOwner
    {
        for(uint x; x < Targets.length; x++) 
        {
            (bool success,) = Targets[x].call{value:(Values[x])}(Datas[x]);
            require(success, "i have failed u anakin");
        }
    }

    /*--------------------------------------------------------*/
    /*------------------ * VIEW FUNCTIONS * ------------------*/
    /*--------------------------------------------------------*/

    /**
     * @dev Returns The Full Claim Struct At The Provided Claim Index
     * @param ClaimIndex The Claim Index To View
     */
    function ViewMerkleClaimerClaim(uint ClaimIndex) external view returns(IMerkle.Claim memory) 
    { 
        return IMerkle(Params._MerkleClaimer).ViewClaim(ClaimIndex); 
    }

    /**
     * @dev Returns An Array Of Claim Structs At The Provided Claim Indexes
     * @param ClaimIndexes The Claim Indexes To View
     */
    function ViewMerkleClaimerClaims(uint[] calldata ClaimIndexes) external view returns (IMerkle.Claim[] memory) 
    { 
        return IMerkle(Params._MerkleClaimer).ViewClaims(ClaimIndexes); 
    }

    /**
     * @dev Returns The Full Project Struct At The Provided Launchpad Project ID
     * @param LaunchpadProjectID The Launchpad Project ID To View
     */
    function ViewLaunchpadProject(uint LaunchpadProjectID) external view returns(ILaunchpadRegistry.Project memory) 
    { 
        return ILaunchpadRegistry(Params._LaunchpadRegistry).ViewProject(LaunchpadProjectID); 
    }

    /**
     * @dev Returns An Array Of Project Structs At The Provided Launchpad Project IDs
     * @param LaunchpadProjectIDs The Launchpad Project IDs To View
     * @return Projects An Array Of Project Structs
     * note: `Projects` Is An Array Of Project Structs
     */
    function ViewLaunchpadProjects(uint[] calldata LaunchpadProjectIDs) external view returns(ILaunchpadRegistry.Project[] memory) 
    { 
        return ILaunchpadRegistry(Params._LaunchpadRegistry).ViewProjects(LaunchpadProjectIDs); 
    }

    /**
     * @dev Returns The SaleID & SaleType At The Provided Launchpad ID
     * @param LaunchpadProjectID The Launchpad Project ID To View
     * @return SaleID The SaleID
     * @return SaleContractAddress The Sale Contract Address
     */
    function ViewSaleAtLaunchpadID(uint LaunchpadProjectID) external view returns (uint SaleID, address SaleContractAddress)
    {
        return ILaunchpadRegistry(Params._LaunchpadRegistry).ViewSaleInfo(LaunchpadProjectID);
    }
}

/*-----------------------------------------------------*/
/*------------------ * INTERFACES * -------------------*/
/*-----------------------------------------------------*/

/**
 * @dev Interface For The Merkle Claimer
 */
interface IMerkle
{
    struct Claim
    {
        string _Name;            // [0] -> _Name
        bytes32 _Root;           // [1] -> _Root
        address _NFT;            // [2] -> _NFT
        address _Operator;       // [3] -> _Operator
        address _BurnNFT;        // [4] -> _BurnNFT
        uint _ClaimCost;         // [5] -> _ClaimCost
        uint _ProjectID;         // [6] -> _ProjectID
        uint _ClaimableAmount;   // [7] -> _ClaimableAmount
        uint _AmountClaimed;     // [8] -> _AmountClaimed
        uint _StartingTimestamp; // [9] -> _StartingTimestamp
        uint _SecondsInterval;   // [10] -> _SecondsInterval
    }

    /**
     * @dev Starts A New Merkle Claim
     */
    function _NewClaim(Claim memory _NewClaim) external returns (uint ClaimIndex);

    /**
     * @dev Overrides A Merkle Claim
     */
    function _OverrideMerkleClaim(uint ClaimIndex, Claim memory _NewClaim) external;

    /**
     * @dev Changes The Current Merkle Root At The Provided Claim Index 
     */
    function _ChangeRoot(uint ClaimIndex, bytes32 Root) external;

    /**
     * @dev Returns The Claim Information For The Provided Claim Index
     */
    function ViewClaim(uint ClaimIndex) external view returns (Claim memory);

    /**
     * @dev Returns Claim Information For The Provided Claim Indexes
     */
    function ViewClaims(uint[] calldata ClaimIndexes) external view returns (Claim[] memory);
}

/**
 * @dev Interface For The Live Mint Smart Contract
 */
interface ILiveMint
{
    struct Params
    {
        string _Name;             // [0] -> _Name   
        address _MintPass;        // [1] -> _MintPass
        address _Minter;          // [2] -> _Minter
        address _PolyptychSource; // [3] -> _PolyptychSource
        uint _MaxSupply;          // [4] -> _MaxSupply
        uint _ArtBlocksProjectID; // [5] -> _ArtBlocksProjectID 
        uint _PolyStart;          // [6] -> _PolyStart
        uint _PolyEnd;            // [7] -> _PolyEnd
    }

    /**
     * @dev Initializes Live Mint & Returns LiveMintProjectID
     */
    function _InitLiveMintArtist ( Params memory ) external returns ( uint );
}

/**
 * @dev Interface For The Marketplace Contracts
 */
interface IMarketplace 
{ 
    struct DutchSale
    {
        string _Name;                     // [0] -> _Name
        uint _ProjectIDArtBlocks;         // [1] -> _ProjectIDArtBlocks
        uint _PriceStart;                 // [2] -> _PriceStart
        uint _PriceEnd;                   // [3] -> _PriceEnd
        uint _MaxAmtPerPurchase;          // [4] -> _MaxAmtPerPurchase
        uint _MaximumAvailableForSale;    // [5] -> _MaximumAvailableForSale
        uint _StartingBlockUnixTimestamp; // [6] -> _StartingBlockUnixTimestamp
        uint _SecondsBetweenPriceDecay;   // [7] -> _SecondsBetweenPriceDecay
        uint _SaleStrip;                  // [8] -> _SaleStrip note: For Traditional MintPack transferFrom() Sales 
    }

    struct DutchSaleInternal
    {
        address _NFT;           // [0] -> _NFT
        address _Operator;      // [1] _Operator (Wallet That NFT Is Pulling From)
        uint _CurrentIndex;     // [2] _CurrentIndex (If Simple Sale Type, This Is The Next Token Index To Iterate Upon)
        uint _Type;             // [3] _SaleType (0 = Simple, 1 = TransferFrom, 2 = PurchaseTo, 3 = MintPack)
        bool _ActivePublic;     // [4] -> _ActivePublic
        bool _ActiveBrightList; // [5] -> _ActiveBrightList 
        bool _Discount;         // [6] -> _Discount
        bool _ActiveRespend;    // [7] -> _ActiveRespend
    }

    struct SalePresale
    {
        string _Name;              // [0] -> _Name
        address _Operator;         // [1] -> _Operator
        address _NFT;              // [2] -> _NFT
        uint _MaxForSale;          // [3] -> _MaxForSale
        uint _MaxPerPurchase;      // [4] -> _MaxPerPurchase
        uint _PricePresale;        // [5] -> _PricePresale
        uint _PricePublic;         // [6] -> _PricePublic
        uint _TimestampEndFullSet; // [7] -> _TimestampEndFullSet
        uint _TimestampEndCitizen; // [8] -> _TimestampEndCitizen
        uint _TimestampSaleStart;  // [9] -> _TimestampSaleStart
        uint _Type;                // [10] -> _Type
        uint _ProjectID;           // [11] -> _ProjectID
    }

    struct SalePresaleRoots
    {
        bytes32 _RootEligibilityFullSet; // [0] -> _RootEligibilityFullSet
        bytes32 _RootEligibilityCitizen; // [1] -> _RootEligibilityCitizen
        bytes32 _RootAmountFullSet;      // [2] -> _RootAmountFullSet
        bytes32 _RootAmountCitizen;      // [3] -> _RootAmountCitizen
    }

    struct SaleFixedPrice
    {
        string _Name;                 // [0] -> _Name
        uint _Price;                  // [1] -> _Price
        uint _MintPassProjectID;      // [2] -> _MintPassProjectID
        uint _Type;                   // [3] -> _Type
        uint _MinterProjectID;        // [4] -> _MinterProjectID
        uint _AmountForSale;          // [5] -> _AmountForSale
        uint _TimestampStart;         // [6] -> _TimestampStart
        uint _CurrentIndex;           // [7] -> _CurrentIndex
        uint _BatchPurchaseAmount;    // [8] -> _BatchPurchaseAmount
        address _NFT;                 // [9] -> _NFT
        address _Operator;            // [10] -> _Operator
        bytes32[] _RootEligibilities; // [11] -> _RootEligibilities
        bytes32[] _RootAmounts;       // [12] -> _RootAmounts
    }

    /**
     * @dev Starts A Sale On The BasicMarketplace Contract
     * @param _Sale The Sale Parameters
     * @param _Roots The Sale Roots
     * @return MarketplaceSaleID
     */
    function _StartPresaleSale(SalePresale memory _Sale, SalePresaleRoots memory _Roots) external returns (uint);

    /**
     * @dev Starts A Fixed Price Sale On The BasicMarketplace Contract
     * @param _Sale The Sale Parameters
     * @param _Discounts The Sale Discounts
     * @return MarketplaceSaleID
     */
    function _StartFixedPriceSale(SaleFixedPrice memory _Sale, uint[] calldata _Discounts) external returns (uint);

    /**
     * @dev Initiates A New Sale On The DutchMarketplace Contract
     * @param _Sale The Sale Parameters
     * @param _State The Sale Internal State
     * @param RootsPrioriy The Merkle Roots For Priority
     * @param RootsAmounts The Merkle Roots For Amounts
     * @param DiscountAmounts The Discount Amounts
     * @return MarketplaceSaleID
     */
    function _StartSale (
        DutchSale memory _Sale, 
        DutchSaleInternal memory _State, 
        bytes32[] calldata RootsPrioriy, 
        bytes32[] calldata RootsAmounts, 
        uint[] calldata DiscountAmounts
    ) external returns ( uint );

    /**
     * @dev Clears A Dutch Auction & Initiates Refunds & Proceeds
     * @param SaleIndex The Index Of The Sale To Initiate Refunds & Proceeds For
     */
    function _InitiateRefundsAndProceeds (uint SaleIndex) external;

    /**
     * @dev Withdraws All ETH From The Marketplace To The Multisig
     */
    function _WithdrawETHFromSaleIndex(uint SaleIndex) external;
}

/**
 * @dev Interface For English Auction Contract
 */
interface IAuction
{
    struct Params
    {
        string _Name;                // [0] -> Name Of Auction
        bool _ClearingEnabled;       // [1] -> If Rebate Last Price Is Enabled (Everyone Pays Lowest Leaderboard Price)
        bool _UserSettlementEnabled; // [2] -> If Self-Service User Settlement Is Enabled (Bidders Can Settle Their Own Bids ETH & NFTs)
        uint _LeaderboardSize;       // [3] -> The Bid Threshold For NewMinimumBid (eg. 50 Valid Bids)
        uint _UnixStartTime;         // [4] -> Unix Start Time Of Auction
        uint _UnixEndTime;           // [5] -> Unix End Time Of Auction
        uint _MinBIPSIncrease;       // [6] -> Minimum BIPS (%) Increase On Each Subsequent Bid After Configured LeaderboardSize 
        uint _SecondsExtension;      // [7] -> # Of Second(s) Of Extension For Auction (Input In Seconds)
        uint _SecondsThreshold;      // [8] -> # Of Seconds Within Auction End Time To Be Eligible For Auction Extension
        uint _InitialMinimumBid;     // [9] -> Initial Minimum Bid
        uint _ProjectIDMintPass;     // [10] -> The Factory MintPass ProjectID
        uint _MintPackAmount;        // [11] -> The Amount Of Mint Passes To Be Dispersed Per Leaderboard Spot
        address _NFT;                // [12] -> Address Of NFT Contract
        address _Operator;           // [13] -> Wallet Holding NFTs To Disperse
    }

    /**
     * @dev Starts Auction
     * @param AuctionParams The Auction Parameters
     * @param MerkleRootsEligibilities The Merkle Roots For Eligibility
     * @param MerkleRootsAmounts The Merkle Roots For Amounts
     * @param DiscountAmounts The Discount Amounts
     * @return AuctionID
     */
    function _StartAuction (
        Params memory AuctionParams, 
        bytes32[] calldata MerkleRootsEligibilities, 
        bytes32[] calldata MerkleRootsAmounts, 
        uint[] calldata DiscountAmounts
    ) external returns (uint);

    /**
     * @dev Processes Auction Proceeds & NFTs (When TokenID Is Ambiguous)
     * @param AuctionID The AuctionID To Process
     */
    function _ProcessETHAndNFTsTokenIDsAmbiguous(uint AuctionID) external;

    /**
     * @dev Processes Auction Proceeds & NFTs (When TokenID Matters)
     * @param AuctionID The AuctionID To Process
     */
    function _ProcessETHAndNFTsTokenIDsDistinct(uint AuctionID) external;

    /**
     * @dev Processes Auction Proceeds & NFTs (When TokenID Is Unique) (Uses Leaderboard Ranking)
     * @param AuctionID The AuctionID To Process
     */
    function _ProcessETHAndNFTsTokenIDsSpecificRanked(uint AuctionID, uint[] calldata TokenIDs) external;

    /**
     * @dev Processes Auction Proceeds & NFTs Mint Pack Style (Uses Leaderboard Ranking)
     * @param AuctionID The AuctionID To Process
     */
    function _ProcessETHAndNFTsMintPacksRanked(uint AuctionID) external;
}

/**
 * @dev Interface For Mint Pass Factory
 */
interface IMintPass
{
    struct Params
    {
        string _Name;              // [0] -> _Name
        string _Symbol;            // [1] -> _Symbol
        string _MetadataURI;       // [2] -> _MetadataURI
        uint _MaxSupply;           // [3] -> _MaxSupply
        uint _MintsPerPack;        // [4] -> _MintsPerPack
        uint _ArtBlocksProjectID;  // [5] -> _ArtBlocksProjectID note: For Cases Where Mint Pass ProjectID 1:1 With ProjectIDs
        uint _ReserveAmount;       // [6] -> _Reserve
        address _Marketplace;      // [7] -> _Marketplace
        address _LiveMint;         // [8] -> _LiveMint
    }

    /**
     * @dev Creates A New Mint Pass Project & Returns The Address Of Deployment
     * @param MintPassParams The Mint Pass Parameters
     * @return MintPassAddress The Contract Address Of The New Mint Pass Project
     */
    function _InitMintPass ( Params memory MintPassParams ) external returns ( address );

    /**
     * @dev Updates The Base URI For A Mint Pass Project
     * @param BaseURI The New BaseURI
     */
    function _NewBaseURI ( string calldata BaseURI ) external;
}

/**
 * @dev Interface For The Launchpad Registry Contract
 */
interface ILaunchpadRegistry
{
    struct Project
    {
        string _Name;                // [0] -> _Name
        bool _Active;                // [1] -> _Active
        uint _LiveMintArtistID;      // [2] -> _LiveMintArtistID
        uint _MaxSupply;             // [3] -> _MaxSupply
        uint _MintsPerPack;          // [4] -> _MintsPerPack
        uint _SaleID;                // [5] -> _SaleID
        address _MintPassAddress;    // [8] -> _MintPassAddress
        address _MarketplaceAddress; // [9] -> _MarketplaceAddress
        address _MintedWorkAddress;  // [10] -> _MintedWorkAddress
    }

    /**
     * @dev Adds A New Project To The Launchpad Registry
     * @param LaunchpadRegistryProject The Launchpad Registry Project Struct
     * @return LaunchpadProjectID
     */
    function _NewProject ( Project memory LaunchpadRegistryProject ) external returns ( uint );

    /**
     * @dev Returns Dutch Auction SaleID Corresponding To LaunchpadProjectID
     * @param LaunchpadProjectID The Launchpad ProjectID
     * @return SaleID
     */
    function ViewSaleIndex ( uint LaunchpadProjectID ) external view returns ( uint SaleID );

    /**
     * @dev Returns Launchpad Project
     * @param LaunchpadProjectID The Launchpad ProjectID
     * @return Launchpad Project
     */
    function ViewProject ( uint LaunchpadProjectID ) external view returns ( Project memory );

    /**
     * @dev Returns Launchpad Projects
     * @param Indexes The Indexes To Return
     * @return Launchpad Projects
     */
    function ViewProjects ( uint[] calldata Indexes ) external view returns ( Project[] memory );

    /**
     * @dev Returns Launchpad Projects In A Specified Range
     * @param StartingIndex The Starting Index
     * @param EndingIndex The Ending Index
     * @return Launchpad Projects
     */
    function ViewProjectsInRange ( uint StartingIndex, uint EndingIndex ) external view returns ( Project[] memory );

    /**
     * @dev Returns Mint Pass Address That Corresponds To Launchpad ProjectID
     * @param LaunchpadProjectID The Launchpad ProjectID
     * @return MintPassAddress
     */
    function ViewMintPassAddress ( uint LaunchpadProjectID ) external view returns ( address );

    /**
     * @dev Returns The SaleID & Sale Contract Address At The Provided Launchpad ID
     * @param LaunchpadProjectID The Launchpad Project ID To View
     * @return SaleID The SaleID
     * @return SaleContractAddress The Sale Contract Address
     */
    function ViewSaleInfo(uint LaunchpadProjectID) external view returns (uint SaleID, address SaleContractAddress);

}

/**
 * @dev Interface For The Waitlist Contract
 */
interface IWaitlist
{
    /**
     * @dev Instantiates A New Waitlist
     * @param Root The Merkle Root To Start With
     * @return WaitlistIndex The Index Of The New Waitlist
     */
    function _NewWaitlist(string calldata Name, bytes32 Root) external returns (uint WaitlistIndex);

    /**
     * @dev Changes The Waitlist Root
     * @param WaitlistIndex The Waitlist Index To Change
     * @param Root The Merkle Root To Change
     */
    function _NewWaitlistRoot(uint WaitlistIndex, bytes32 Root) external;

    /**
     * @dev Returns A Waitlist At `WaitlistIndex`
     * @param WaitlistIndex The Index Of The Waitlist To Return
     * @return Waitlist The Waitlist At The Provided Index
     */
    function ReadWaitlist(uint WaitlistIndex) external view returns (address[] memory);
}

/**
 * @dev Interface To Upgrade The Launchpad Contract
 */
interface ICustom 
{ 
    /**
     * @dev Upgrades The Launchpad Contract
     * @param NewAddress The New Launchpad Contract Address
     */
    function ____LaunchpadOverrideAddress ( address NewAddress ) external; 
}